#!/usr/bin/perl
# Script to collect variant call files from an Ion Torrent run into a central collectedVariants folder within 
# the main results folder.  This script requires a sampleKey file indicating the barcode and the sample it 
# represents. 
#
# 11/7/2013 - Updated for TSv4.0
# 4/22/2015 - Updated for TSv4.4.2
#
# Created: 2/25/2013	Dave Sims
###################################################################################################################
use warnings;
use strict;
use autodie;

use Cwd qw(abs_path getcwd); 
use File::Basename;
use File::Copy;
use Getopt::Long qw( :config auto_abbrev bundling no_ignore_case );
use Data::Dump;

my $scriptname = basename($0);
my $version = "v3.3.0_090815";
my $description = <<"EOT";
Script to collect variant calls from an Ion Torrent run into a central 'collectedVariants' directory located
within the main results folder.  This script requires a sampleKey consisting of the barcode and sample it
represents, which can be generated by running 'sampleKeyGen'.
EOT

my $usage = <<"EOT";
USAGE: $scriptname [options] <TVC Output Path>
    -o, --outdir        Custom output directory in which to write the results.
    -s, --samplekey     Custom sample key file	
    -v, --version       Version Information
    -h  --help          Display the Help information
EOT

my $ver_info = 0;
my $help = 0;
my $custom_key;
#my $TVCout = "/variantCaller_out";
#my $tvc_out;
my $outdir; 

GetOptions( "outdir|o=s"         => \$outdir, 
            #"tvc_results|t=s"    => \$TVCout,
            #"tvc_results|t=s"    => \$tvc_out,
            "samplekey|s=s"      => \$custom_key,
            "version|v"          => \$ver_info,
            "help|h"             => \$help )
        or print $usage;

sub help {
	printf "%s - %s\n\n%s\n\n%s\n", $scriptname, $version, $description, $usage;
	exit;
}

sub version {
	printf "%s - %s\n", $scriptname, $version;
	exit;
}

help if $help;
version if $ver_info;

#########------------------------------ END ARG Parsing ---------------------------------#########
#
# Check the directory
my $resultsDir = getcwd;
my $tvc_out = shift;
print "dir: $tvc_out\n";
#TODO
die "ERROR: You must provide the TVC Plugin results directory to process!\n" unless $tvc_out;
#my $tvc_path = "$resultsDir/plugin_out/$TVCout";

#print "path: $tvc_path\n";

#if ( ! -d $tvc_path ) { 
#die "ERROR: Either you are not running this script from a run results folder, or TVC has not yet been run!\n" unless (-d $tvc_path); 
die "ERROR: Either you are not running this script from a run results folder, or TVC has not yet been run!\n" unless (-d $tvc_out); 
#}
my ($runid) = $resultsDir =~ /([PM]C[123C]-\d+)/;

# Make collectedVariants directory
# Don't need this if it's called from a wrapper script.
if ( ! $outdir ) {
    $outdir = "$resultsDir/collectedVariants";
    my ($plugin_number) = $tvc_out =~ /\.(\d+)\/?$/;
    $outdir .= ".$plugin_number" if $plugin_number;
}

my $sampleKey;
# Allow for custom sample key in order to run standalone
if ( defined $custom_key ) {
	$sampleKey = $custom_key;
} else {
	$sampleKey = "$outdir/sampleKey.txt";
}

# Check to make sure we have a valid sampleKey and load up a list
open( my $skey_fh, "<", "$sampleKey" ) || die "No sample key found: $!\n";
#my %barcodes = map { chomp; split(/\t/) } <$skey_fh>;
my %barcodes;
while (<$skey_fh>) {
    chomp;
    my ($bc, $samp) = split( /\t/ );
    $samp =~ tr/ /_/;
    $barcodes{$bc} = $samp;
}
close( $skey_fh );

# Read list of IonXpress dirs in TVC results dir
opendir( my $tvc_files, $tvc_out) || die "Can't read TVC results dir: $!\n";
my @sample_dirs = grep { $barcodes{$_} } readdir($tvc_files);
closedir $tvc_files;

# Sanity check to make sure that the sampleKey is a good match for the data available
my $sampleNumber = 0;
$sampleNumber = scalar( @sample_dirs ) if @sample_dirs; 
my $numBC = keys %barcodes;
if ( $sampleNumber == 0  || ( $sampleNumber + 1 ) < $numBC ) { # Have to add 1 to sample number to account for LibNTC
	print "WARNING: Data not in good agreement with sample key:\n\n";
	print "\t\t'$sampleNumber' matches out of '$numBC' barcodes\n\n";
	print "Are you sure you want to continue [y/N]? ";
	chomp( my $ans = <STDIN> );
	if ( $ans =~ /^n|N$/ || $ans eq "" ) {
		die "Check for non-conforming BC index in Experiment Plan. Exiting....\n";
	} 
	elsif ($ans =~ /^y|Y$/ ) {
		print "Continuing on then. Check to be sure all samples are represented in the final output results\n";
	}
	else {
		die "Not a yes or no answer. Exiting to be safe\n";
	}
}

# Move to the variantCaller_out directory and grab copies of the variants files  Switch to the alleles.xls table as variants.xls is deprecated.
#chdir( $tvc_path );
chdir( $tvc_out);
foreach my $sample ( @sample_dirs ) {
    my $tab_file = "$sample/alleles.xls"; # replaces variants.xls
	my $vcf_file = "$sample/TSVC_variants.vcf";

	# Grab alleles.xls files
	if ( -f $tab_file ) {
		copy( $tab_file, "$outdir/$sample.txt" ); #make copy of unfiltered data
		filter_vars( \$tab_file ); #filter and add to collectedVariants dir
	} 
	else {
		warn "The tabular variant call file: '$sample/alleles.xls' can not be found! $!\n";
	}

	# Grab vcf files
	if ( -f $vcf_file ) {
		copy( $vcf_file, "$outdir/$barcodes{$sample}_${runid}.vcf" ); 
		filter_vars( \$vcf_file );
	} else {
		warn "No TSVC_variants.vcf file found.  Skipping...\n";
	}
}

# Create a summary table based on the data generated from above.
summary_table();

sub filter_vars {
	# Get rid of the 'Absent' and 'No Call' data to make more informative output.  Will keep raw data for troubleshooting later  
	my $varfile = shift;

	my @filtered_data;
	my ($barcode) = $$varfile =~ /^(IonXpress_\d+)/;
	my $sample_name = $barcodes{$barcode};

    # Setup the headers here for use downstream
    my $data_format = "%-7s %-12s %-8s %-16s %-6s %-12s %-12s %8.2f %12.0f %8.0f %8.0f %8.0f %5.0f     %-12s\n";
    my $theader_format = "%-7s %-12s %-8s %-16s %-6s %-12s %-12s   %-8s      %-6s    %-6s   %-7s %-8s %-5s %-12s\n";
    my @tab_fields = qw{ Chrom Position Gene AmpID Type Ref Alt Freq Qual Cov RefCov VarCov HP Hotspot };
    my $tab_header = sprintf( $theader_format, @tab_fields );

	if ( $$varfile =~ /\.vcf$/ ) {
        my $outfile = "$outdir/${sample_name}_${runid}_filtered.vcf";
        return;
	}
    elsif ( $$varfile =~ /\.xls$/ ) {
		# Filter alleles.xls 
		open( my $xls_fh, "<", $$varfile ) || die "Can't open the variants.xls file for reading";
		@filtered_data =  grep { ! /(Absent|No Call)/ } <$xls_fh>; # Get only variant containing lines

        my $outfile = "$outdir/${sample_name}_filtered.txt";

        open( my $out_fh, ">", $outfile ) || die "Can't create file '$outfile' for writing: $!";
        print $out_fh $tab_header;

		for ( @filtered_data ) {
			my @data = split;
            next if /Chrom/;

            my $refcov = $data[18]-$data[24];
            my @subset = ( @data[0,14,12,13,9,2,3,6,7,18], $refcov, @data[24,37,11] );
            my $formatted_data = sprintf( $data_format, @subset );

            print $out_fh $formatted_data;
		} 
    }
	return;
}

sub summary_table {
    # Create a summary table of all of the variants found in all of the samples.  Need to work with a filtered
	# dataset or this file will be a mess!
	
    my $theader_format = "%-7s %-12s %-8s %-16s %-6s %-12s %-12s   %-8s      %-6s    %-6s   %-7s %-8s %-5s %-12s\n";
    my @tab_fields = qw{ Chrom Position Gene AmpID Type Ref Alt Freq Qual Cov RefCov VarCov HP Hotspot };
    my $tab_header = sprintf( $theader_format, @tab_fields );

	#my $outputFile = "$outdir" .  $runid . "_allVariants.tsv";
	my $outputFile = "${outdir}/${runid}_allVariants.tsv";
	open ( my $summary_fh, ">", $outputFile ) || die "Output file: '$outputFile' can not be opened for writing! $!";

	opendir( my $vars_dir, "$outdir" ) || die "Can't read collectedVariants directory! $!";
    my @var_files = map { "$barcodes{$_}_filtered.txt" } map { $_ =~ /(IonXpress_\d+)\.txt/ } readdir($vars_dir);

	# Read through all the variant call files, concatenate them, and add the sample name to the row.
	foreach my $varFile ( sort( @var_files ) ) {
        my ($sample_name) = $varFile =~ /^(.*)_filtered.*/;
		open ( my $varfile, "<", "$outdir/$varFile" ) || die "Can't open this file for some reason: '$varFile'\n";
		print $summary_fh "::: Variant Calls for $sample_name :::\n\n";
        print $summary_fh "$tab_header";

        print $summary_fh "$sample_name\t$_" while (<$varfile>);
        print $summary_fh "\n";
	}
    return;
}
